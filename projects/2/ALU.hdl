// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN
        x[16], y[16],  // 16-bit inputs
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // zero x
    Mux16(a=x, b=false, sel=zx, out=x1);

    // negate x
    Not16(in=x1, out=notX);
    Mux16(a=x1, b=notX, sel=nx, out=x2);

    // zero y
    Mux16(a=y, b=false, sel=zy, out=y1);

    // negate y
    Not16(in=y1, out=notY);
    Mux16(a=y1, b=notY, sel=ny, out=y2);

    // f on?
    Add16(a=x2, b=y2, out=xPlusY);
    And16(a=x2, b=y2, out=xAndY);
    Mux16(a=xAndY, b=xPlusY, sel=f, out=tempResult);

    // negate output
    Not16(in=tempResult, out=notResult);
    Mux16(a=tempResult, b=notResult, sel=no, out[0..7]=resLSB, out[8..14]=resMid, out[15]=resMSB);

    // zero result
    Or8Way(in=resLSB, out=or1);
    Or8Way(in[0..6]=resMid, in[7]=resMSB, out=or2);
    Or(a=or1, b=or2, out=notZR);
    Not(in=notZR, out=zr);

    // negative result
    And(a=resMSB, b=true, out=ng);

    // output
    And16(a=true, b[0..7]=resLSB, b[8..14]=resMid, b[15]=resMSB, out=out);
}
